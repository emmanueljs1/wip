{-# OPTIONS --sized-types #-}

import Relation.Binary.PropositionalEquality as Eq
open import Data.Empty using (âŠ¥)
open import Data.Product using (_,_; _Ã—_; âˆƒ-syntax)
open import Data.Sum using (_âŠ_; injâ‚; injâ‚‚)
open import Data.Unit using (âŠ¤; tt)
open import Relation.Unary using (_âˆˆ_)
open import Size
open Eq using (_â‰¡_; refl)

module Delay where

variable i : Size

data Type : Set where
  ğŸš : Type
  _â‡’_ : Type â†’ Type â†’ Type

infixr 7 _â‡’_
variable S T : Type

data Ctx : Set where
  Îµ : Ctx
  _â€¢_ : Ctx â†’ Type â†’ Ctx

infixl 5 _â€¢_
variable Î“ : Ctx

data _âˆ‹_ : Ctx â†’ Type â†’ Set where
  zero : Î“ â€¢ T âˆ‹ T
  suc : Î“ âˆ‹ T â†’ Î“ â€¢ S âˆ‹ T

infix 4 _âˆ‹_
variable x : Î“ âˆ‹ T

data _âŠ¢_ : Ctx â†’ Type â†’ Set where
  yes no : Î“ âŠ¢ ğŸš
  var : Î“ âˆ‹ T â†’ Î“ âŠ¢ T
  _âˆ™_ : Î“ âŠ¢ S â‡’ T â†’ Î“ âŠ¢ S â†’ Î“ âŠ¢ T
  Æ›_ : Î“ â€¢ S âŠ¢ T â†’ Î“ âŠ¢ S â‡’ T


infix 4 _âŠ¢_
variable r s t : Î“ âŠ¢ T

mutual
  data Value : Type â†’ Set where
    yes no : Value ğŸš
    âŸ¨_âŸ©_ : Î“ âŠ¢ T â†’ Env Î“ â†’ Value T
    wrong : Value T

  data Env : Ctx â†’ Set where
    Îµ : Env Îµ
    _â€¢_ : Env Î“ â†’ Value T â†’ Env (Î“ â€¢ T)

infix 5 âŸ¨_âŸ©_
variable a b f : Value T
variable Î³ Î´ : Env Î“

_??_ : Env Î“ â†’ Î“ âˆ‹ T â†’ Value T
Î³ â€¢ a ?? zero = a
Î³ â€¢ a ?? suc x = Î³ ?? x

infix 4 _??_

mutual
  data Delay (i : Size) (A : Set) : Set where
    now : A â†’ Delay i A
    later : âˆDelay i A â†’ Delay i A

  record âˆDelay (i : Size) (A : Set) : Set where
    coinductive
    field
      force : âˆ€ {j : Size< i} â†’ Delay j A

open âˆDelay

mutual
  _>>=_ : âˆ€ {A B : Set} â†’ Delay i A â†’ (A â†’ Delay i B) â†’ Delay i B
  now a >>= f = f a
  later aâˆ >>= f = later (aâˆ âˆ>>= f)

  _âˆ>>=_ : âˆ€ {A B : Set} â†’ âˆDelay i A â†’ (A â†’ Delay i B) â†’ âˆDelay i B
  force (aâˆ âˆ>>= f) = force aâˆ >>= f

infix 5 _>>=_

data _â‡“_ {A : Set} : Delay âˆ A â†’ A â†’ Set where
  â‡“now : âˆ€ {a : A} â†’ now a â‡“ a
  â‡“later : âˆ€ {a : A} {aâˆ : âˆDelay âˆ A} â†’ force aâˆ â‡“ a â†’ later aâˆ â‡“ a

infix 4 _â‡“_

_â‡“ : âˆ€ {A : Set} â†’ Delay âˆ A â†’ Set
a? â‡“ = âˆƒ[ a ] a? â‡“ a

bindâ‡“ : âˆ€ {A B : Set} {f : A â†’ Delay âˆ B}
          {a? : Delay âˆ A} {a : A} {b : B}
        â†’ a? â‡“ a â†’ f a â‡“ b â†’ a? >>= f â‡“ b
bindâ‡“ â‡“now a?â‡“a = a?â‡“a
bindâ‡“ (â‡“later aâˆâ‡“a) a?â‡“a = â‡“later (bindâ‡“ aâˆâ‡“a a?â‡“a)

mutual
  apply : Value (S â‡’ T) â†’ Value S â†’ Delay i (Value T)
  apply (âŸ¨ Æ› t âŸ© Î´) a = later (beta t Î´ a)
  apply (âŸ¨ var _ âŸ© _) _ = now wrong
  apply (âŸ¨ _ âˆ™ _ âŸ© _) _ = now wrong
  apply wrong _ = now wrong

  beta : Î“ â€¢ S âŠ¢ T â†’ Env Î“ â†’ Value S â†’ âˆDelay i (Value T)
  force (beta t Î´ a) = eval t (Î´ â€¢ a)

  eval : Î“ âŠ¢ T â†’ Env Î“ â†’ Delay i (Value T)
  eval yes _ = now yes
  eval no _ = now no
  eval (var x) Î³ = now (Î³ ?? x)
  eval (r âˆ™ s) Î³ =
    eval r Î³ >>= Î» f â†’ eval s Î³ >>= apply f
  eval (Æ› t) Î³ = now (âŸ¨ Æ› t âŸ© Î³)

mutual
  ğ’±[_] : (T : Type) â†’ Value T â†’ Set
  ğ’±[ ğŸš ] yes = âŠ¤
  ğ’±[ ğŸš ] no = âŠ¤
  ğ’±[ S â‡’ T ] f =
    âˆ€ {a : Value S}
    â†’ a âˆˆ ğ’±[ S ]
    â†’ apply f a âˆˆ ğ’Ÿ[ T ]
  ğ’±[ _ ] _ = âŠ¥

  ğ’Ÿ[_] : (T : Type) â†’ Delay âˆ (Value T) â†’ Set
  ğ’Ÿ[ T ] a? = âˆƒ[ a ] a? â‡“ a Ã— a âˆˆ ğ’±[ T ]

_âŠ¨_ : (Î“ : Ctx) â†’ Env Î“ â†’ Set
Îµ âŠ¨ Îµ = âŠ¤
Î“ â€¢ T âŠ¨ Î³ â€¢ a = Î“ âŠ¨ Î³ Ã— a âˆˆ ğ’±[ T ]

infix 4 _âŠ¨_

semantic-typing : Î“ âŠ¢ T â†’ Set
semantic-typing {Î“} {T} t =
  âˆ€ {Î³ : Env Î“}
  â†’ Î“ âŠ¨ Î³
  â†’ eval t Î³ âˆˆ ğ’Ÿ[ T ]

infix 4 semantic-typing

syntax semantic-typing {Î“} {T} t = Î“ âŠ¨ t âˆ· T

fundamental-lemma : âˆ€ (t : Î“ âŠ¢ T) â†’ Î“ âŠ¨ t âˆ· T
fundamental-lemma yes âŠ¨Î³ = yes , â‡“now , tt
fundamental-lemma no âŠ¨Î³ = no , â‡“now , tt
fundamental-lemma (var zero) {Î³ â€¢ a} (_ , aâˆˆğ’±) = a , â‡“now , aâˆˆğ’±
fundamental-lemma (var (suc x)) {Î³ â€¢ _} (âŠ¨Î³ , _) = fundamental-lemma (var x) âŠ¨Î³
fundamental-lemma (r âˆ™ s) âŠ¨Î³
  with fundamental-lemma r âŠ¨Î³ | fundamental-lemma s âŠ¨Î³
...  | f , â‡“f , fâˆˆğ’±           | a , â‡“a , aâˆˆğ’±
  with fâˆˆğ’± aâˆˆğ’±
...  | b , â‡“b , bâˆˆğ’± =
  b , bindâ‡“ â‡“f (bindâ‡“ â‡“a â‡“b) , bâˆˆğ’±
fundamental-lemma (Æ› t) {Î³} âŠ¨Î³ =
  âŸ¨ Æ› t âŸ© Î³  ,
  â‡“now ,
  Î» aâˆˆğ’± â†’
    let (f , â‡“f , fâˆˆğ’±) = fundamental-lemma t (âŠ¨Î³ , aâˆˆğ’±) in
    f , â‡“later â‡“f , fâˆˆğ’±

type-soundness : (t : Îµ âŠ¢ ğŸš) â†’ eval t Îµ â‡“ yes âŠ eval t Îµ â‡“ no
type-soundness t
  with fundamental-lemma t tt
... | yes , â‡“yes , _ = injâ‚ â‡“yes
... | no , â‡“no , _ = injâ‚‚ â‡“no
